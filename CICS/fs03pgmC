      *----------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *----------------------------------------------------------------*
       PROGRAM-ID.                         FS03PGMC.
       AUTHOR.                             FELIPE.

      *----------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *----------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.

      *----------------------------------------------------------------*
       DATA                                DIVISION.
      *----------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *----------------------------------------------------------------*
       77  WS-MSG-ERRO                     PIC X(80).
       77  WS-LENGTH                       PIC S9(04) COMP.

       01  WS-VAR-TEMPO.
           05 WS-DATA                      PIC X(10).
           05 WS-HORARIO                   PIC X(08).

       01  WS-DFHCOMMAREA.
           05 WS-FASE                      PIC X(01).
           05 ws-codprod-commarea          pic x(04).
           COPY FS03CAD.
           COPY DFHAID.
           COPY DFHBMSCA.
      * VARIAVEL ESPELHO DO VCADPRD
       01  WS-REG-VCADPRD.
           05 WS-CODPROD                   PIC X(04).
           05 WS-DESCPROD                  PIC X(20).
           05 WS-UNIDPROD                  PIC X(05).
           05 WS-LOCALPROD                 PIC X(05).
           05 WS-QTDEST                    PIC 9(05).
           05 WS-QTDMAX                    PIC 9(05).
           05 WS-QTDMIN                    PIC 9(05).
           05 WS-PRECOCOMPRA               PIC 9(06)V99.
           05 WS-PRECOVENDA                PIC 9(06)V99.
           05 WS-PERCOMIS                  PIC 9(02)V99.
      *----------------------------------------------------------------*
       LINKAGE                             SECTION.
      *----------------------------------------------------------------*
       01  DFHCOMMAREA.
           05 OCCURS 0 TO 24579 TIMES DEPENDING ON EIBCALEN
                                           PIC X(01).
      *----------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *----------------------------------------------------------------*
           EXEC CICS HANDLE CONDITION
              NOTFND(999-NOTFND)
              MAPFAIL(999-MAPFAIL)
              ERROR(999-ERROR)
           END-EXEC

      * SELETOR DE FASE - A CONSULTA POSSUI 3 FASES
      *    FASE 1 - ENVIA O MAPA PARA O TERMINAL
      *    FASE 2 - TRATA O CAMPO T2COD
      *    FASE 3 - TRATA O CAMPO T2conf
      *
           MOVE DFHCOMMAREA                TO WS-DFHCOMMAREA

           IF EIBCALEN EQUAL 0
              MOVE +80                     TO WS-LENGTH
              MOVE 'UTILIZE A OPCA C DA TRANSACAO'
                                           TO WS-MSG-ERRO
                 PERFORM 999-ENCERRA-TRANSACAO
           END-IF

           EVALUATE WS-FASE
              WHEN '1' PERFORM 100-FASE1
              WHEN '2' PERFORM 200-FASE2
              WHEN '3' PERFORM 300-FASE3
              WHEN 'P' PERFORM 999-PAGINACAO-CONSULTAR
              WHEN OTHER
                 MOVE +80                  TO WS-LENGTH
                 MOVE 'ERRO NO NUMERO DA FASE'
                                           TO WS-MSG-ERRO
                 PERFORM 999-ENCERRA-TRANSACAO
           END-EVALUATE
           .

       100-FASE1.
           MOVE LOW-VALUES                 TO MAPACADO
           MOVE -1                         TO T2CODL
           MOVE 'entre o codigo do produto para excluir'
                                           TO T2MSGO
           perform 999-protecao-FASE2
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE2
           .

       200-FASE2.
           EXEC CICS HANDLE AID
              ENTER   (210-ENTER)
              PF3     (220-PF3)
              PF5     (230-PF5)
              CLEAR   (230-PF5)
              ANYKEY  (240-ANYKEY)
           END-EXEC

           EXEC CICS RECEIVE
              MAP   ('MAPACAD')
              MAPSET('FS03CAD')
              INTO  (MAPACADI)
           END-EXEC
           .

       210-ENTER.
           IF T2CODL EQUAL 0 OR T2CODI EQUAL SPACES
              MOVE 'CODIGO NAO INFORMADOR TENTE NOVAMENTE'
                                           TO T2MSGO
              PERFORM 999-TRATA-FASE2
           END-IF

           EXEC CICS READ
             FILE('VCADPRD')
             RIDFLD(T2CODI)
             INTO(WS-REG-VCADPRD)
           END-EXEC

      * MOVER OS CAMPOS DO REGISTRO PARA A TELA
           MOVE WS-CODPROD                 TO T2CODO
           MOVE WS-DESCPROD                TO T2DESCO
           MOVE WS-UNIDPROD                TO T2UNIDO
           MOVE WS-LOCALPROD               TO T2LOCALO
           MOVE WS-QTDEST                  TO T2ESTO
           MOVE WS-QTDMAX                  TO T2MAXO
           MOVE WS-QTDMIN                  TO T2MINO
           MOVE WS-PRECOCOMPRA             TO T2COMPO
           MOVE WS-PRECOVENDA              TO T2VENDO
           MOVE WS-PERCOMIS                TO T2COMISO

           PERFORM 999-TRATA-FASE3
           .

        220-PF3.
           MOVE '1'                        to ws-fase

           EXEC CICS XCTL
             PROGRAM ('FS03PGMM')
             COMMAREA(Ws-DFHCOMMAREA)
             LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

        230-PF5.
           PERFORM 999-CHAMA-FASE1
           .

        240-ANYKEY.
           MOVE 'TECLA PRESSIONADA INVALIDA!'
                                           TO T2MSGO
           PERFORM 999-TRATA-FASE2
           .

        300-FASE3.
           EXEC CICS HANDLE AID
              ENTER   (310-ENTER)
              PF3     (320-PF3)
              PF5     (330-PF5)
              CLEAR   (330-PF5)
              ANYKEY  (340-ANYKEY)
           END-EXEC

           EXEC CICS RECEIVE
              MAP   ('MAPACAD')
              MAPSET('FS03CAD')
              INTO  (MAPACADI)
           END-EXEC
           .

        310-ENTER.
           IF T2CONFL EQUAL 0       OR
              T2CONFI NOT EQUAL 'S' AND 'N'
              MOVE 'DIGITE APENAS S(SIM) OU N(NAO)'
                                           TO T2MSGO
              PERFORM 999-TRATA-FASE3
           END-IF

           IF T2CONFI = 'S'
              EXEC CICS DELETE
                 FILE('VCADPRD')
                 RIDFLD(T2CODI)
              END-EXEC
           END-IF

           PERFORM 320-PF3
           .

       320-PF3.
           MOVE '1'                        TO WS-FASE

           EXEC CICS XCTL
               PROGRAM('FS03PGMM')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       330-PF5.
           PERFORM 999-CHAMA-FASE1
           .

       340-ANYKEY.
           MOVE 'TECLA PRESSIONADA INVALIDA!'
                                           TO T2MSGO
           PERFORM 999-TRATA-FASE3
           .

       999-PROTECAO-FASE2.
      * DESPROTEGER
           MOVE  DFHUNIMD                  TO T2CODA
      * PROTEGER
           MOVE DFHBMPRF                   TO T2DESCA
           MOVE DFHBMPRF                   TO T2UNIDA
           MOVE DFHBMPRF                   TO T2LOCALA
           MOVE DFHBMPRF                   TO T2ESTA
           MOVE DFHBMPRF                   TO T2MAXA
           MOVE DFHBMPRF                   TO T2MINA
           MOVE DFHBMPRF                   TO T2COMPA
           MOVE DFHBMPRF                   TO T2VENDA
           MOVE DFHBMPRF                   TO T2COMISA
           MOVE DFHBMPRF                   TO T2CONFA

      * UNDERLINE
           MOVE DFHUNDLN                   TO T2CODH
      * TIRAR UNDERLINE
           MOVE DFHALL                     TO T2CONFH
           .

       999-PROTECAO-FASE3.
      * DESPROTEGER
           MOVE DFHUNIMD                   TO T2CONFA
      * PROTEGER
           MOVE DFHBMPRF                   TO T2CODA
           MOVE DFHBMPRF                   TO T2DESCA
           MOVE DFHBMPRF                   TO T2UNIDA
           MOVE DFHBMPRF                   TO T2LOCALA
           MOVE DFHBMPRF                   TO T2ESTA
           MOVE DFHBMPRF                   TO T2MAXA
           MOVE DFHBMPRF                   TO T2MINA
           MOVE DFHBMPRF                   TO T2COMPA
           MOVE DFHBMPRF                   TO T2VENDA
           MOVE DFHBMPRF                   TO T2COMISA

      * UNDERLINE
           MOVE DFHUNDLN                   TO T2CODH
      * TIRAR UNDERLINE
           MOVE DFHALL                     TO T2CONFH
           .

        999-ENCERRA-TRANSACAO.
            EXEC CICS SEND TEXT
               FROM (WS-MSG-ERRO)
               LENGTH(WS-LENGTH)
               ERASE FREEKB ALARM
            END-EXEC

            EXEC CICS RETURN
            END-EXEC
            .

       999-MANDA-TELA.
            MOVE EIBTRMID                  TO T2TERMO
            MOVE EIBTRNID                  TO T2TRANO
            MOVE EIBTASKN                  TO T2TASKO
            MOVE WS-FASE                   TO T2FASEO

            MOVE 'EXCLUSAO VSAM-KSDS'      TO T2TELAO

            EXEC CICS ASSIGN
               USERID(T2USERO)
            END-EXEC

            EXEC CICS LINK
                PROGRAM('AUXCICS1')
                COMMAREA(WS-VAR-TEMPO)
                LENGTH(+18)
            END-EXEC

            MOVE WS-DATA                   TO T2DATAO
            MOVE WS-HORARIO                TO T2HORAO

            EXEC CICS SEND
               MAP ('MAPACAD')
               MAPSET('FS03CAD')
               FROM(MAPACADO)
               ERASE FREEKB ALARM CURSOR
            END-EXEC
            .

       999-CHAMA-FASE1.
            MOVE '1'                       TO WS-FASE

            EXEC CICS XCTL
                PROGRAM('FS03PGMa')
                COMMAREA(WS-DFHCOMMAREA)
                LENGTH(LENGTH OF WS-DFHCOMMAREA)
            END-EXEC
            .

       999-CHAMA-FASE2.
            MOVE '2'                       TO WS-FASE

            EXEC CICS RETURN
                TRANSID('F03C')
                COMMAREA(WS-DFHCOMMAREA)
                LENGTH(LENGTH OF WS-DFHCOMMAREA)
            END-EXEC
            .
       999-CHAMA-FASE3.
           MOVE '3'                        TO WS-FASE
           EXEC CICS RETURN
               TRANSID('F03C')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .
        999-TRATA-FASE2.
           MOVE -1                         TO T2CODL
           PERFORM 999-PROTECAO-FASE2
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE2
           .
        999-TRATA-FASE3.
           MOVE -1                         TO T2CONFL
      *          1234567890123456789012345
           MOVE 'CONSULTAR OUTRO (S/N)?'   TO T2ACAOO

      * MOVER OS CAMPOS DO REGISTRO PARA A TELA
           MOVE WS-CODPROD                 TO T2CODO
           MOVE WS-DESCPROD                TO T2DESCO
           MOVE WS-UNIDPROD                TO T2UNIDO
           MOVE WS-LOCALPROD               TO T2LOCALO
           MOVE WS-QTDEST                  TO T2ESTO
           MOVE WS-QTDMAX                  TO T2MAXO
           MOVE WS-QTDMIN                  TO T2MINO
           MOVE WS-PRECOCOMPRA             TO T2COMPO
           MOVE WS-PRECOVENDA              TO T2VENDO
           MOVE WS-PERCOMIS                TO T2COMISO

           PERFORM 999-PROTECAO-FASE3
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE3

           .
       999-NOTFND.
           IF WS-FASE = '2'
              STRING 'PRODUTO '            DELIMITED BY SIZE
                      T2CODI               DELIMITED BY SPACE
                      ' NAO LOCALIZADO'    DELIMITED BY SIZE
                                           INTO T2MSGO

              MOVE LOW-VALUES              TO T2CODO
           ELSE
              MOVE T2CODI                  TO WS-CODPROD
              MOVE LOW-VALUES              TO MAPACADO
              STRING 'PRODUTO '            DELIMITED BY SIZE
                      WS-CODPROD           DELIMITED BY SPACE
                      ' EXCLUIDO POR OUTRO USUARIO '
                                           DELIMITED BY SIZE
                                           INTO T2MSGO
           END-IF

           PERFORM 999-TRATA-FASE2
           .

        999-MAPFAIL.
           MOVE 'ERRO MAPA FS03CAD'        TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .

        999-ERROR.
           MOVE 'ERRO GENERICO FS03PGMC'   TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .

        999-PAGINACAO-CONSULTAR.
           MOVE +4                         TO T2CODL
           MOVE WS-CODPROD-COMMAREA        TO T2CODO
           PERFORM 210-ENTER
           .
