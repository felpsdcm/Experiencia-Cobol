      *----------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *----------------------------------------------------------------*
       PROGRAM-ID.                         FS03PGMG.
       AUTHOR.                             FELIPE.

      *----------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *----------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.

      *----------------------------------------------------------------*
       DATA                                DIVISION.
      *----------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *----------------------------------------------------------------*
       77  WS-MSG-ERRO                     PIC X(80).
       77  WS-LENGTH                       PIC S9(04) COMP.

       01  WS-VAR-TEMPO.
           05 WS-DATA                      PIC X(10).
           05 WS-HORARIO                   PIC X(08).

       01  WS-DFHCOMMAREA.
           05 WS-FASE                      PIC X(01).
       77  WS-SQLCODE                      PIC X(10).
       77  WS-CODPROD                      PIC X(04).

           COPY FS03CAD.
           COPY DFHAID.
           COPY DFHBMSCA.

           EXEC SQL
              INCLUDE BKTBCAD
           END-EXEC.

           EXEC SQL
              INCLUDE SQLCA
           END-EXEC
      *----------------------------------------------------------------*
       LINKAGE                             SECTION.
      *----------------------------------------------------------------*
       01  DFHCOMMAREA.
           05 OCCURS 0 TO 24576 TIMES DEPENDING ON EIBCALEN
                                           PIC X(01).
      *----------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *----------------------------------------------------------------*
           EXEC CICS HANDLE CONDITION
              MAPFAIL (999-MAPFAIL)
              ERROR   (999-ERROR  )
           END-EXEC

      * SELETOR DE FASE - A CONSULTA POSSUI 4 FASES
      *    FASE 1 - ENVIA O MAPA PARA O TERMINAL
      *    FASE 2 - TRATA O CAMPO T2COD (CHAVE)
      *    FASE 3 - TRATA OS CAMPOS DE DADOS (NAO CHAVE)
      *    FASE 4 - TRATA O CAMPO T2CONF
      *
           MOVE DFHCOMMAREA                TO WS-DFHCOMMAREA

           IF EIBCALEN EQUAL 0
              MOVE +80                     TO WS-LENGTH
              MOVE 'UTILIZE A OPCAO "G" DA TRANSACAO F03M'
                                           TO WS-MSG-ERRO
                 PERFORM 999-ENCERRA-TRANSACAO
           END-IF

           EVALUATE WS-FASE
              WHEN '1' PERFORM 100-FASE1
              WHEN '2' PERFORM 200-FASE2
              WHEN '3' PERFORM 300-FASE3
              WHEN '4' PERFORM 400-FASE4
              WHEN OTHER
                 MOVE +80                  TO WS-LENGTH
                 MOVE 'ERRO NO NUMERO DA FASE'
                                           TO WS-MSG-ERRO
                 PERFORM 999-ENCERRA-TRANSACAO
           END-EVALUATE
           .

       100-FASE1.
           MOVE LOW-VALUES                 TO MAPACADO
           MOVE -1                         TO T2CODL
           MOVE 'ENTRE O CODIGO DO PRODUTO PARA INCLUIR'
                                           TO T2MSGO
           PERFORM 999-PROTECAO-FASE2
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE2
           .

       200-FASE2.
           EXEC CICS HANDLE AID
              ENTER   (210-ENTER)
              PF3     (220-PF3)
              PF5     (230-PF5)
              CLEAR   (230-PF5)
              ANYKEY  (240-ANYKEY)
           END-EXEC

           EXEC CICS RECEIVE
              MAP   ('MAPACAD')
              MAPSET('FS03CAD')
              INTO  (MAPACADI)
           END-EXEC
           .

       210-ENTER.
           IF T2CODL EQUAL 0 OR T2CODI EQUAL SPACES
              MOVE 'CODIGO NAO INFORMADO - TENTE NOVAMENTE'
                                           TO T2MSGO
              PERFORM 999-TRATA-FASE2
           END-IF

           MOVE T2CODI                     TO CODPROD

           EXEC SQL
              SELECT CODPROD
                    ,DESCPROD
                    ,UNIDPROD
                    ,LOCALPROD
                    ,QTDEST
                    ,QTDMAX
                    ,QTDMIN
                    ,PRECOCOMPRA
                    ,PRECOVENDA
                    ,PRECOMIS
              INTO  :CODPROD
                   ,:DESCPROD
                   ,:UNIDPROD
                   ,:LOCALPROD
                   ,:QTDEST
                   ,:QTDMAX
                   ,:QTDMIN
                   ,:PRECOCOMPRA
                   ,:PRECOVENDA
                   ,:PRECOMIS
              FROM TBPRODUTO
              WHERE CODPROD = :CODPROD
           END-EXEC

           IF SQLCODE = 0
              STRING 'O PRODUTO '  DELIMITED BY SIZE
                     T2CODI        DELIMITED BY SPACES
                     ' JA EXISTE.' DELIMITED BY SIZE
                                   INTO T2MSGO

              MOVE LOW-VALUES                 TO T2CODO

              PERFORM 999-TRATA-FASE2
           ELSE
              IF SQLCODE = +100
                 PERFORM 999-NOTFND
              ELSE
                 MOVE SQLCODE                 TO WS-SQLCODE
                 STRING 'ERRO DB2 SQLCODE = ' DELIMITED BY SIZE
                        WS-SQLCODE DELIMITED BY SIZE
                        INTO WS-MSG-ERRO
                 MOVE +80 TO WS-LENGTH
                 PERFORM 999-ENCERRA-TRANSACAO
              END-IF
           END-IF
           .

       220-PF3.
           MOVE '1'                        TO WS-FASE

           EXEC CICS XCTL
               PROGRAM('FS03PGMM')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       230-PF5.
           PERFORM 999-CHAMA-FASE1
           .

       240-ANYKEY.
           MOVE 'TECLA PRESSIONADA INVALIDA!'
                                           TO T2MSGO
           PERFORM 999-TRATA-FASE2
           .

       300-FASE3.
           EXEC CICS HANDLE AID
              ENTER   (310-ENTER)
              PF3     (320-PF3)
              PF5     (330-PF5)
              CLEAR   (330-PF5)
              ANYKEY  (340-ANYKEY)
           END-EXEC

           EXEC CICS RECEIVE
              MAP   ('MAPACAD')
              MAPSET('FS03CAD')
              INTO  (MAPACADI)
           END-EXEC
           .

       310-ENTER.
      * LIMPAR OS CAMPOS NUMERICOS DE CARACTERES ALFANUMERICOS
           EXEC CICS BIF DEEDIT FIELD(T2ESTI  ) END-EXEC
           EXEC CICS BIF DEEDIT FIELD(T2MAXI  ) END-EXEC
           EXEC CICS BIF DEEDIT FIELD(T2MINI  ) END-EXEC
           EXEC CICS BIF DEEDIT FIELD(T2COMPI ) END-EXEC
           EXEC CICS BIF DEEDIT FIELD(T2VENDI ) END-EXEC
           EXEC CICS BIF DEEDIT FIELD(T2COMISI) END-EXEC

      * VERIFICAR SE ALGUM CAMPO SEM PREENCHER
           IF T2DESCL  = 0 OR T2DESCI  EQUAL SPACES OR
              T2UNIDL  = 0 OR T2UNIDI  EQUAL SPACES OR
              T2LOCALL = 0 OR T2LOCALI EQUAL SPACES OR
              T2ESTL   = 0 OR
              T2MAXL   = 0 OR
              T2MINL   = 0 OR
              T2COMPL  = 0 OR
              T2VENDL  = 0 OR
              T2COMISL
              MOVE 'FAVOR PREENCHER TODOS OS CAMPOS'
                                           TO T2MSGO
              PERFORM 999-TRATA-FASE3
           END-IF

      * VERIFICAR SE MAX > MIN
           IF T2MAXI <= T2MINI
              MOVE 'QTD. MAX. DEVE SER MAIOR QUE QTD. MIN.'
                                           TO T2MSGO
              PERFORM 999-TRATA-FASE3
           END-IF

           PERFORM 999-TRATA-FASE4
           .

       320-PF3.
           MOVE '1'                        TO WS-FASE

           EXEC CICS XCTL
               PROGRAM('FS03PGMM')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       330-PF5.
           PERFORM 999-CHAMA-FASE1
           .

       340-ANYKEY.
           MOVE 'TECLA PRESSIONADA INVALIDA!'
                                           TO T2MSGO
           PERFORM 999-TRATA-FASE3
           .

       400-FASE4.
           EXEC CICS HANDLE AID
              ENTER   (410-ENTER)
              PF3     (420-PF3)
              PF5     (430-PF5)
              CLEAR   (430-PF5)
              ANYKEY  (440-ANYKEY)
           END-EXEC

           EXEC CICS RECEIVE
              MAP   ('MAPACAD')
              MAPSET('FS03CAD')
              INTO  (MAPACADI)
           END-EXEC
           .

       410-ENTER.
           IF T2CONFL EQUAL 0       OR
              T2CONFI NOT EQUAL 'S' AND 'N'
              MOVE 'DIGITE APENAS S(SIM) OU N(NAO)'
                                           TO T2MSGO
              PERFORM 999-TRATA-FASE4
           END-IF

           IF T2CONFI = 'S'
              MOVE T2CODI                  TO CODPROD
              MOVE T2DESCI                 TO DESCPROD-TEXT
              MOVE +20                     TO DESCPROD-LEN
              MOVE T2UNIDI                 TO UNIDPROD
              MOVE T2LOCALI                TO LOCALPROD
              MOVE T2ESTI                  TO QTDEST
              MOVE T2MAXI                  TO QTDMAX
              MOVE T2MINI                  TO QTDMIN
              MOVE T2COMPI                 TO PRECOCOMPRA
              MOVE T2VENDI                 TO PRECOVENDA
              MOVE T2COMISI                TO PRECOMIS

              EXEC SQL
                 INSERT INTO TBPRODUTO
                 (  CODPROD
                   ,DESCPROD
                   ,UNIDPROD
                   ,LOCALPROD
                   ,QTDEST
                   ,QTDMAX
                   ,QTDMIN
                   ,PRECOCOMPRA
                   ,PRECOVENDA
                   ,PRECOMIS
                 )
                 VALUES
                 ( :CODPROD
                  ,:DESCPROD
                  ,:UNIDPROD
                  ,:LOCALPROD
                  ,:QTDEST
                  ,:QTDMAX
                  ,:QTDMIN
                  ,:PRECOCOMPRA
                  ,:PRECOVENDA
                  ,:PRECOMIS
                 )
              END-EXEC
           END-IF

           IF SQLCODE = 0
              PERFORM 420-PF3
           ELSE
              IF SQLCODE = -803
                 PERFORM 999-DUPREC
              ELSE
                 MOVE SQLCODE              TO WS-SQLCODE
                 STRING 'ERRO DB2 SQLCODE = ' DELIMITED BY SIZE
                        WS-SQLCODE DELIMITED BY SIZE
                        INTO WS-MSG-ERRO
                 MOVE +80 TO WS-LENGTH
                 PERFORM 999-ENCERRA-TRANSACAO
              END-IF
           END-IF
           .

       420-PF3.
           MOVE '1'                        TO WS-FASE

           EXEC CICS XCTL
               PROGRAM('FS03PGMM')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       430-PF5.
           PERFORM 999-CHAMA-FASE1
           .

       440-ANYKEY.
           MOVE 'TECLA PRESSIONADA INVALIDA!'
                                           TO T2MSGO
           PERFORM 999-TRATA-FASE4
           .

       999-PROTECAO-FASE2.
      * DESPROTEGER
           MOVE DFHUNIMD                   TO T2CODA

      * PROTEGER
           MOVE DFHBMPRF                   TO T2DESCA
           MOVE DFHBMPRF                   TO T2UNIDA
           MOVE DFHBMPRF                   TO T2LOCALA
           MOVE DFHBMPRF                   TO T2ESTA
           MOVE DFHBMPRF                   TO T2MAXA
           MOVE DFHBMPRF                   TO T2MINA
           MOVE DFHBMPRF                   TO T2COMPA
           MOVE DFHBMPRF                   TO T2VENDA
           MOVE DFHBMPRF                   TO T2COMISA
           MOVE DFHBMPRF                   TO T2CONFA

      * UNDERLINE
           MOVE DFHUNDLN                   TO T2CODH

      * TIRAR UNDERLINE
           MOVE DFHALL                     TO T2DESCH
           MOVE DFHALL                     TO T2UNIDH
           MOVE DFHALL                     TO T2LOCALH
           MOVE DFHALL                     TO T2ESTH
           MOVE DFHALL                     TO T2MAXH
           MOVE DFHALL                     TO T2MINH
           MOVE DFHALL                     TO T2COMPH
           MOVE DFHALL                     TO T2VENDH
           MOVE DFHALL                     TO T2COMISH
           MOVE DFHALL                     TO T2CONFH
           .

       999-PROTECAO-FASE3.
      * DESPROTEGER
           MOVE DFHUNIMD                   TO T2DESCA
           MOVE DFHUNIMD                   TO T2UNIDA
           MOVE DFHUNIMD                   TO T2LOCALA
           MOVE DFHUNIMD                   TO T2ESTA
           MOVE DFHUNIMD                   TO T2MAXA
           MOVE DFHUNIMD                   TO T2MINA
           MOVE DFHUNIMD                   TO T2COMPA
           MOVE DFHUNIMD                   TO T2VENDA
           MOVE DFHUNIMD                   TO T2COMISA

      * PROTEGER
           MOVE DFHBMPRF                   TO T2CODA
           MOVE DFHBMPRF                   TO T2CONFA

      * UNDERLINE
           MOVE DFHUNDLN                   TO T2DESCH
           MOVE DFHUNDLN                   TO T2UNIDH
           MOVE DFHUNDLN                   TO T2LOCALH
           MOVE DFHUNDLN                   TO T2ESTH
           MOVE DFHUNDLN                   TO T2MAXH
           MOVE DFHUNDLN                   TO T2MINH
           MOVE DFHUNDLN                   TO T2COMPH
           MOVE DFHUNDLN                   TO T2VENDH
           MOVE DFHUNDLN                   TO T2COMISH

      * TIRAR UNDERLINE
           MOVE DFHALL                     TO T2CODH
           MOVE DFHALL                     TO T2CONFH
           .

       999-PROTECAO-FASE4.
      * DESPROTEGER
           MOVE DFHUNIMD                   TO T2CONFA
      * PROTEGER
           MOVE DFHBMPRF                   TO T2CODA
           MOVE DFHBMPRF                   TO T2DESCA
           MOVE DFHBMPRF                   TO T2UNIDA
           MOVE DFHBMPRF                   TO T2LOCALA
           MOVE DFHBMPRF                   TO T2ESTA
           MOVE DFHBMPRF                   TO T2MAXA
           MOVE DFHBMPRF                   TO T2MINA
           MOVE DFHBMPRF                   TO T2COMPA
           MOVE DFHBMPRF                   TO T2VENDA
           MOVE DFHBMPRF                   TO T2COMISA
      * UNDERLINE
           MOVE DFHUNDLN                   TO T2CONFH
      * TIRAR O UNDERLINE
           MOVE DFHALL                     TO T2CODH
           MOVE DFHALL                     TO T2DESCH
           MOVE DFHALL                     TO T2UNIDH
           MOVE DFHALL                     TO T2LOCALH
           MOVE DFHALL                     TO T2ESTH
           MOVE DFHALL                     TO T2MAXH
           MOVE DFHALL                     TO T2MINH
           MOVE DFHALL                     TO T2COMPH
           MOVE DFHALL                     TO T2VENDH
           MOVE DFHALL                     TO T2COMISH
           .

       999-MANDA-TELA.
            MOVE EIBTRMID                  TO T2TERMO
            MOVE EIBTRNID                  TO T2TRANO
            MOVE EIBTASKN                  TO T2TASKO
            MOVE WS-FASE                   TO T2FASEO

            MOVE 'INCLUSAO DB2'            TO T2TELAO

            EXEC CICS ASSIGN
               USERID(T2USERO)
            END-EXEC

            EXEC CICS LINK
                PROGRAM('AUXCICS1')
                COMMAREA(WS-VAR-TEMPO)
                LENGTH(+18)
            END-EXEC

            MOVE WS-DATA                   TO T2DATAO
            MOVE WS-HORARIO                TO T2HORAO

            EXEC CICS SEND
               MAP ('MAPACAD')
               MAPSET('FS03CAD')
               FROM(MAPACADO)
               ERASE FREEKB ALARM CURSOR
            END-EXEC
            .

       999-CHAMA-FASE1.
           MOVE '1'                        TO WS-FASE

           EXEC CICS XCTL
               PROGRAM('FS03PGMG')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       999-CHAMA-FASE2.
           MOVE '2'                        TO WS-FASE

           EXEC CICS RETURN
               TRANSID('F03G')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       999-CHAMA-FASE3.
           MOVE '3'                        TO WS-FASE

           EXEC CICS RETURN
               TRANSID('F03G')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       999-CHAMA-FASE4.
           MOVE '4'                        TO WS-FASE

           EXEC CICS RETURN
               TRANSID('F03G')
               COMMAREA(WS-DFHCOMMAREA)
               LENGTH(LENGTH OF WS-DFHCOMMAREA)
           END-EXEC
           .

       999-TRATA-FASE2.
           MOVE -1                         TO T2CODL
           PERFORM 999-PROTECAO-FASE2
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE2
           .

       999-TRATA-FASE3.
           MOVE -1                         TO T2DESCL
           PERFORM 999-PROTECAO-FASE3
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE3
           .

       999-TRATA-FASE4.
           MOVE -1                         TO T2CONFL
      *          1234567890123456789012345
           MOVE 'CONFIRMA INCLUSAO (S/N)?' TO T2ACAOO

           PERFORM 999-PROTECAO-FASE4
           PERFORM 999-MANDA-TELA
           PERFORM 999-CHAMA-FASE4
           .

       999-NOTFND.
           MOVE 'INFORME OS DADOS DO PRODUTO PARA INSERIR'
                                           TO T2MSGO
           PERFORM 999-TRATA-FASE3
           .

       999-DUPREC.
           MOVE T2CODI                     TO WS-CODPROD

           MOVE LOW-VALUES                 TO MAPACADO

           STRING 'O PRODUTO '  DELIMITED BY SIZE
                  WS-CODPROD    DELIMITED BY SPACES
                  ' FOI INSERIDO POR OUTRO USUARIO' DELIMITED BY SIZE
                                 INTO T2MSGO


           PERFORM 999-TRATA-FASE2
           .

       999-ENCERRA-TRANSACAO.
            EXEC CICS SEND TEXT
               FROM (WS-MSG-ERRO)
               LENGTH(WS-LENGTH)
               ERASE FREEKB ALARM
            END-EXEC

            EXEC CICS RETURN
            END-EXEC
            .


       999-MAPFAIL.
           MOVE +80                        TO WS-LENGTH
           MOVE 'ERRO MAPA FS03CAD'        TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .

       999-ERROR.
           MOVE +80                        TO WS-LENGTH
           MOVE 'ERRO GENERICO FS03PGMG'   TO WS-MSG-ERRO
           PERFORM 999-ENCERRA-TRANSACAO
           .
