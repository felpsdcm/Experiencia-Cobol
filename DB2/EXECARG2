      *--------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *--------------------------------------------------------------*
       PROGRAM-ID.                         EXECARG2.
       AUTHOR.                             FELIPE.
      *
      *--------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *--------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *
       INPUT-OUTPUT                        SECTION.
      *
       FILE-CONTROL.
           SELECT CADPRDD1 ASSIGN TO UT-S-CADPRDD1
           FILE STATUS IS FS-CADPRDD1
           .
      *--------------------------------------------------------------*
       DATA                                DIVISION.
      *--------------------------------------------------------------*
       FILE                                SECTION.
      *--------------------------------------------------------------*
       FD  CADPRDD1
           RECORDING MODE IS F.

           COPY BKCADPRD REPLACING ==::== BY ====.

      *--------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *--------------------------------------------------------------*
      * VARIAVEIS ESPELHO DOS REGISTROS                              *
      *--------------------------------------------------------------*
           COPY BKCADPRD REPLACING ==::== BY ==WS-==.

           COPY VARDATA.
           COPY VARTIME.

      *    BOOK DAS VARIAVEIS HOSPEDEIRAS
           EXEC SQL
               INCLUDE BKTBCAD
           END-EXEC
           .
      *    BOOK DAS VARIAVEIS PARA USO DO SQL
           EXEC SQL
               INCLUDE SQLCA
           END-EXEC


      *--------------------------------------------------------------*
      * VARIAVEIS DE FILE STATUS                                     *
      *--------------------------------------------------------------*
       01  FS-CADPRDD1                     PIC X(02).
           88 SUCESSO-CAD                          VALUE "00".
           88 FIM-ARQ-CAD                          VALUE "10".

       77  WS-SQLCODE                      PIC +9(9).
      *
      *--------------------------------------------------------------*
      * VARIAVEIS DE TRABALHO                                        *
      *--------------------------------------------------------------*

       77  WS-CTLIDO                       PIC 9(04) COMP.
       77  WS-CTINS                        PIC 9(04) COMP.
       77  WS-CTDUP                        PIC 9(04) COMP.
       77  WS-CTINV                        PIC 9(04) COMP.
      *--------------------------------------------------------------*
      * VARIAVEIS COM MASCARA DE EDICAO (FORMATADAS)                 *
      *--------------------------------------------------------------*
       77  WS-CTLIDO-F                     PIC ZZZ9.
       77  WS-CTINS-F                      PIC ZZZ9.
       77  WS-CTDUP-F                      PIC ZZZ9.
       77  WS-CTINV-F                      PIC ZZZ9.
      *
      *--------------------------------------------------------------*

      *--------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *--------------------------------------------------------------*

           PERFORM 1000-INICIALIZAR
           PERFORM 2000-PROCESSAR UNTIL FIM-ARQ-CAD
           PERFORM 3000-TERMINO
           STOP RUN
           .

       1000-INICIALIZAR.
           ACCEPT WS-HORARIO-INICIAL FROM TIME

           MOVE 0                           TO WS-CTLIDO
                                               WS-CTINS
                                               WS-CTDUP
                                               WS-CTINV

           OPEN INPUT CADPRDD1
           IF NOT SUCESSO-CAD
               DISPLAY "DEU RUIM PA ABRIR O CADPRDD1 FIO"
               DISPLAY FS-CADPRDD1
               GO TO 9000-ERRO
           END-IF

           PERFORM 1500-LER-CADPRDD1
           .

       1500-LER-CADPRDD1.
           READ CADPRDD1 INTO WS-REG-CADPRDD1
           IF SUCESSO-CAD
               ADD 1                         TO WS-CTLIDO
           ELSE
               IF NOT FIM-ARQ-CAD
                   DISPLAY "DEU RUIM PA LER O CADPRDD1 FIO"
                   DISPLAY FS-CADPRDD1
                   GO TO 9000-ERRO
               END-IF
           END-IF
           .

       2000-PROCESSAR.
      *----------------------------------------------------------------*
      *    MOVER DADOS DA VARIAVELS ESPELHO PARA HOSPEDEIRA            *
      *----------------------------------------------------------------*
           MOVE WS-CODPROD-D1                TO CODPROD
           MOVE +20                          TO DESCPROD-LEN
           MOVE WS-DESCPROD-D1               TO DESCPROD-TEXT
           MOVE WS-UNIDPROD-D1               TO UNIDPROD
           MOVE WS-LOCALPROD-D1              TO LOCALPROD
           MOVE WS-QTDEST-D1                 TO QTDEST
           MOVE WS-QTDMAX-D1                 TO QTDMAX
           MOVE WS-QTDMIN-D1                 TO QTDMIN
           MOVE WS-PRECOCOMPRA-D1            TO PRECOCOMPRA
           MOVE WS-PRECOVENDA-D1             TO PRECOVENDA
           MOVE WS-PRECOMIS-D1               TO PRECOMIS

      *----------------------------------------------------------------*
      *    INSERIR OS DADSO DA HOSPEDEIRA NA TABELA(SQL FIELDS)        *
      *----------------------------------------------------------------*
           EXEC SQL
               INSERT INTO TBPRODUTO
               (   CODPROD,
                   DESCPROD,
                   UNIDPROD,
                   LOCALPROD,
                   QTDEST,
                   QTDMAX,
                   QTDMIN,
                   PRECOCOMPRA,
                   PRECOVENDA,
                   PRECOMIS
               )
               VALUES
               (   :CODPROD,
                   :DESCPROD,
                   :UNIDPROD,
                   :LOCALPROD,
                   :QTDEST,
                   :QTDMAX,
                   :QTDMIN,
                   :PRECOCOMPRA,
                   :PRECOVENDA,
                   :PRECOMIS
               )
           END-EXEC

      *----------------------------------------------------------------*
      *    TESTAR SQLCODE                                              *
      *----------------------------------------------------------------*
           EVALUATE SQLCODE
           WHEN 0
               ADD 1 TO WS-CTINS
           WHEN -803
               ADD 1 TO WS-CTDUP
           WHEN -545
               ADD 1 TO WS-CTINV
           WHEN OTHER
               DISPLAY "ERRO INSERT TBCLIENTE"
               DISPLAY SQLCODE
               GO TO 9000-ERRO-DB2
           END-EVALUATE

           PERFORM 1500-LER-CADPRDD1
           .

       3000-TERMINO.
           CLOSE CADPRDD1
           IF NOT SUCESSO-CAD
               DISPLAY "DEU POBREMA NO FECHAMENTO PARCEIRO"
               DISPLAY FS-CADPRDD1
               GO TO 9000-ERRO
           END-IF

           ACCEPT WS-HORARIO-FINAL FROM TIME

           PERFORM 9000-CALCULA-TEMPO-PROC

           MOVE WS-CTLIDO                     TO WS-CTLIDO-F
           MOVE WS-CTINS                      TO WS-CTINS-F
           MOVE WS-CTDUP                      TO WS-CTDUP-F
           MOVE WS-CTINV                      TO WS-CTINV-F

           PERFORM 9000-IMPRIME-DATA


           DISPLAY '=================================================='
           DISPLAY '        ESTATISCA FINAL DE PROCESSAMENTO          '
           DISPLAY '=================================================='
           DISPLAY 'QTDE. CLIENTES LIDOS CADCLI.........: ' WS-CTLIDO-F
           DISPLAY 'QTDE. CLIENTES INSERIDOS TBCLIENTE..: ' WS-CTINS-F
           DISPLAY 'QTDE. CLIENTES DUPLICADOS...........: ' WS-CTDUP-F
           DISPLAY 'QTDE. CLIENTES INVALIDOS............: ' WS-CTINV-F
           DISPLAY '=================================================='
           DISPLAY 'TEMPO TOTAL DE PROCESSAMENTO...'
           DISPLAY WS-TEMPO-PROCESSAMENTO
           DISPLAY '=================================================='
           DISPLAY '             TERMINO NORMAL EXECARGA2             '
           DISPLAY '=================================================='
           .
      *
       9000-ERRO-DB2.
           DISPLAY '=================================================='
           DISPLAY 'CODIGO...: ' WS-SQLCODE
           DISPLAY '=================================================='
           .

       9000-ERRO.
           DISPLAY '=================================================='
           DISPLAY '==         ERMINO ANORMAL DO EXECARGA2          =='
           DISPLAY '=================================================='
           STOP RUN
           .

           COPY ROTDATA.
           COPY ROTPROC.
