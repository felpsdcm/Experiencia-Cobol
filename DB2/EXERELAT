      *--------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *-------------------------------------------------------------*
       PROGRAM-ID.                         EXERELAT.
       AUTHOR.                             FELIPE.
      *
      *--------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *--------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *
       INPUT-OUTPUT                        SECTION.
      *
       FILE-CONTROL.
           SELECT CLIRELAT ASSIGN TO UT-S-CLIRELAT
           FILE STATUS IS FS-CLIRELAT
           .
      *--------------------------------------------------------------*
       DATA                                DIVISION.
      *--------------------------------------------------------------*
       FILE                                SECTION.
      *--------------------------------------------------------------*
       FD  CLIRELAT
           LABEL RECORD OMITTED
           RECORDING MODE IS F
           RECORD CONTAINS 132 CHARACTERS
           DATA RECORD IS REG-CLIRELAT
           .
       01  REG-CLIRELAT                    PIC X(132).

      *--------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *--------------------------------------------------------------*


      *    BOOK DAS VARIAVEIS HOSPEDEIRAS
           EXEC SQL
               INCLUDE BKEXETC
           END-EXEC
           .

      *    BOOK DAS VARIAVEIS PARA USO DO SQL
           EXEC SQL
               INCLUDE SQLCA
           END-EXEC

       01  WS-CONTADORES-COMP.
           05  WS-CTLIDO                   PIC 9(05) COMP.
           05  WS-CTIMP                    PIC 9(05) COMP.
           05  WS-CTPAG                    PIC 9(05) COMP.
           05  WS-CTLIN                    PIC 9(05) COMP.
           05  WS-PULA                     PIC 9(05) COMP.

       01  WS-CONTADORES-COMP-F.
           05  WS-CTLIDO-F                 PIC ZZZ9.
           05  WS-CTIMP-F                  PIC ZZZ9.
           05  WS-CTPAG-F                  PIC ZZZ9.
           05  WS-CTLIN-F                  PIC ZZZ9.
      *---------------------------------------------------------------*
      *  LAYOUT DO RELATORIO                                          *
      *---------------------------------------------------------------*
       01  WS-CABEC1.
           05 WS-DATA-CABEC1               PIC X(10).
           05 FILLER                       PIC X(01)
                                               VALUE SPACES.
           05 WS-HORA-CABEC1               PIC X(08).
           05 FILLER                       PIC X(40)
                                               VALUE SPACES.
           05 FILLER                       PIC X(30)
                                               VALUE
              "RELACAO DE CLIENTES SEM E-MAIL".

           05 FILLER                       PIC X(35)
                                               VALUE SPACES.

           05 FILLER                       PIC X(05)
                                               VALUE "PAG. ".
           05 WS-PAG-CABEC1                PIC ZZ9.



       01  WS-CABEC2.
           05 FILLER                       PIC X(132)
                                               VALUE ALL "-".

       01  WS-CABEC3.
           05 FILLER                       PIC X(33)
                                               VALUE
              "  CODIGO        NOME             ".

           05 FILLER                       PIC X(33)
                                               VALUE
              "                     ENDERECO    ".

           05 FILLER                       PIC X(33)
                                               VALUE
              "                                 ".


           05 FILLER                       PIC X(33)
                                               VALUE
              "   TELEFONE         CPF          ".


       01  WS-CABEC4.
           05 FILLER                       PIC X(33)
                                                   VALUE
              "  ------        -----------------".
           05 FILLER                       PIC X(33)
                                                   VALUE
              "-------------        ------------".
           05 FILLER                       PIC X(33)
                                                   VALUE
              "----------------------------     ".
           05 FILLER                       PIC X(33)
                                                   VALUE
              "   --------        ------------  ".

      *----------------------------------------------------------------*
      *    DETALHES                                                    *
      *----------------------------------------------------------------*
       01  WS-LINDET.
           05 FILLER                       PIC X(02)
                                                   VALUE SPACES.

           05 LD-CODCLI                    PIC X(04).
           05 FILLER                       PIC X(06)
                                                   VALUE SPACES.

           05 LD-NOMECLI                   PIC X(30).
           05 FILLER                       PIC X(05)
                                                   VALUE SPACES.

           05 LD-ENDCLI                    PIC X(40).
           05 FILLER                       PIC X(05)
                                                   VALUE SPACES.

           05 LD-FONECLI                   PIC X(09).
           05 FILLER                       PIC X(05)
                                                   VALUE SPACES.

           05 LD-CPFCLI                    PIC X(11).
           05 FILLER                       PIC X(05)
                                                   VALUE SPACES.


           05 LD-SALDOCLI                  PIC ZZ.ZZ9,99
                                                   BLANK WHEN ZERO.
           05 FILLER                       PIC X(01)
                                                   VALUE SPACES.

      *----------------------------------------------------------------*
      *    RODAPE                                                      *
      *----------------------------------------------------------------*
       01  WS-RODAPE1                      PIC X(132)
                                                   VALUE ALL "-".
       01  WS-RODAPE2.
           05 FILLER                       PIC X(33)
                                                   VALUE
              "          FEITO COM AMOR         ".
           05 FILLER                       PIC X(33)
                                                   VALUE
              "    POR DROIDES GELADOS DE ACO   ".
           05 FILLER                       PIC X(33)
                                                   VALUE
              "          WEYLAND-YUTANI         ".
           05 FILLER                       PIC X(33)
                                                   VALUE
              "   CONSTRUINDO MUNDOS MELHORES   ".


      *--------------------------------------------------------------*
      * VARIAVEIS DE FILE STATUS                                     *
      *--------------------------------------------------------------*
       01  FS-CLIRELAT                     PIC X(02).
           88 SUCESSO                              VALUE '00'.
           88 FIM-ARQ                              VALUE '10'.
      *--------------------------------------------------------------*
      * VARIAVEIS DE DATA                                            *
      *--------------------------------------------------------------*
       01  WS-DATA-SYS.
           05 WS-ANO                       PIC 9(02).
           05 WS-MES                       PIC 9(02).
           05 WS-DIA                       PIC 9(02).

       01  WS-DATA-FORMATADA.
           05 WS-DIA                       PIC 9(02).
           05 FILLER                       PIC X(01) VALUE "/".

           05 WS-MES                       PIC 9(02).
           05 FILLER                       PIC X(03) VALUE "/20".

           05 WS-ANO                       PIC 9(02).

       01 WS-HORA-SYS.
           05 WS-HORA                      PIC 9(02).
           05 WS-MINUTO                    PIC 9(02).
           05 WS-SEGUNDO                   PIC 9(02).

       01  WS-HORA-FORMATADA.
           05 WS-HORA                      PIC 9(02).
           05 FILLER                       PIC X(01) VALUE ":".

           05 WS-MINUTO                    PIC 9(02).
           05 FILLER                       PIC X(01) VALUE "/".

           05 WS-SEGUNDO                   PIC 9(02).

       01  WS-INDICADORES.
           05 IENDCLI                      PIC S9(04) COMP.
           05 IFONECLI                     PIC S9(04) COMP.
           05 ICPFCLI                      PIC S9(04) COMP.
           05 ISALDOCLI                    PIC S9(04) COMP.
       77  WS-SQLCODE                      PIC +9(09).
           COPY VARDATA.
           COPY VARTIME.
      *--------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *--------------------------------------------------------------*
       0000-MAIN.
           DISPLAY '0000-MAIN'
           PERFORM 1000-INICIALIZAR
           PERFORM 2000-PROCESSAR
               UNTIL SQLCODE = +100
           PERFORM 3000-TERMINO
           GOBACK
           .
           
       1000-INICIALIZAR.
           DISPLAY '1000-INICIALIZAR'
           ACCEPT WS-HORARIO-INICIAL FROM TIME

           INITIALIZE WS-CONTADORES-COMP

           MOVE 99                         TO WS-CTLIN

           ACCEPT WS-DATA-SYS FROM DATE
           ACCEPT WS-HORA-SYS FROM TIME
           MOVE CORRESPONDING WS-DATA-SYS TO WS-DATA-FORMATADA
           MOVE CORRESPONDING WS-HORA-SYS TO WS-HORA-FORMATADA

      * DECLARACAO DO CURSOS PARA SELECIONAR SEM EMAIL

           EXEC SQL
               DECLARE CLISEMEMAIL CURSOR FOR
                   SELECT CODCLI,
                          NOMECLI,
                          ENDCLI,
                          FONECLI,
                          CPFCLI,
                          SALDOCLI
                   FROM TBCLIENTE
                   WHERE EMAILCLI IS NULL
           END-EXEC

      * ABERTURA DO CURSOR
           EXEC SQL
               OPEN CLISEMEMAIL
           END-EXEC

           IF SQLCODE NOT EQUAL 0
               DISPLAY "ERRO AO FECHAR CURSOR"
               DISPLAY SQLCODE
               GO TO 9000-ERRO-DB2
           END-IF

           OPEN OUTPUT CLIRELAT

           IF NOT SUCESSO
               DISPLAY "ERRO ABERTURA CLIRELAT"
               DISPLAY FS-CLIRELAT
               GO TO 9000-ERRO
           END-IF

           PERFORM 1500-LER-CLISEMEMAIL
           .

       1500-LER-CLISEMEMAIL.
           DISPLAY '1500-LER-CLISEMEMAIL'
           EXEC SQL
               FETCH CLISEMEMAIL
               INTO :CODCLI,
                    :NOMECLI,
                    :ENDCLI      :IENDCLI,
                    :FONECLI     :IFONECLI,
                    :CPFCLI      :ICPFCLI,
                    :SALDOCLI    :ISALDOCLI
           END-EXEC

           IF SQLCODE = 0
               ADD 1 TO WS-CTLIDO
               PERFORM 1600-TRATA-INDICATOR
           ELSE
               IF SQLCODE NOT = +100
                   DISPLAY "ERRO DE LEITURA CURSOS CLISEMEMAIL"
                   MOVE SQLCODE    TO WS-SQLCODE
                   GO TO 9000-ERRO-DB2
               END-IF
           END-IF
           .

       1600-TRATA-INDICATOR.
           DISPLAY '1600-INDICATOR'
           IF IENDCLI < 0
               MOVE ALL "*"                 TO ENDCLI-TEXT
           END-IF

           IF IFONECLI < 0
               MOVE ALL "*"                 TO FONECLI
           END-IF

           IF ICPFCLI < 0
               MOVE ALL "*"                 TO CPFCLI
           END-IF

           IF ISALDOCLI < 0
               MOVE ZEROS                   TO SALDOCLI
           END-IF
           .

       2000-PROCESSAR.
           DISPLAY '2000-PROCESSAR'
           IF WS-CTLIN   > 59
               PERFORM 2100-IMPRIME-CABECALHO
           END-IF

           PERFORM 2200-IMPRIME-DETALHE

           IF WS-CTLIN   = 58
               PERFORM 2300-IMPRIME-RODAPE
           END-IF

           PERFORM 1500-LER-CLISEMEMAIL
           .

       2100-IMPRIME-CABECALHO.
           DISPLAY '2100-IMPRIME-CABECALHO'
           MOVE WS-DATA-FORMATADA           TO WS-DATA-CABEC1
           MOVE WS-HORA-FORMATADA           TO WS-HORA-CABEC1
           ADD 1                            TO WS-CTPAG
           MOVE WS-CTPAG                    TO WS-PAG-CABEC1

           WRITE REG-CLIRELAT FROM WS-CABEC1 AFTER PAGE
           IF NOT SUCESSO
               DISPLAY "ERRO IMPRESSAO CABEC1"
               DISPLAY FS-CLIRELAT
               GO TO 9000-ERRO
           END-IF

           WRITE REG-CLIRELAT FROM WS-CABEC2
           IF NOT SUCESSO
               DISPLAY "ERRO IMPRESSAO CABEC2"
               DISPLAY FS-CLIRELAT
               GO TO 9000-ERRO
           END-IF

           WRITE REG-CLIRELAT FROM WS-CABEC3
           IF NOT SUCESSO
               DISPLAY "ERRO IMPRESSAO CABEC3"
               DISPLAY FS-CLIRELAT
               GO TO 9000-ERRO
           END-IF

           WRITE REG-CLIRELAT FROM WS-CABEC4
           IF NOT SUCESSO
               DISPLAY "ERRO IMPRESSAO CABEC4"
               DISPLAY FS-CLIRELAT
               GO TO 9000-ERRO
           END-IF

           MOVE 4                          TO WS-CTLIN
           .

       2200-IMPRIME-DETALHE.
           DISPLAY '2200-IMPRIME-DETALHE'
           MOVE CODCLI                  TO LD-CODCLI
           MOVE NOMECLI-TEXT            TO LD-NOMECLI
           MOVE ENDCLI-TEXT             TO LD-ENDCLI
           MOVE FONECLI                 TO LD-FONECLI
           MOVE CPFCLI                  TO LD-CPFCLI
           MOVE SALDOCLI                TO LD-SALDOCLI

           WRITE REG-CLIRELAT FROM WS-LINDET
           IF NOT SUCESSO
               DISPLAY "ERRO NA IMPRESSAO DO DETALHE"
               DISPLAY FS-CLIRELAT
               GO TO 9000-ERRO
           END-IF

           ADD 1 TO WS-CTLIN
           ADD 1 TO WS-CTIMP
           .

       2300-IMPRIME-RODAPE.
           DISPLAY '2300-IMPRIME-RODAPE'
           COMPUTE WS-PULA = 59 - WS-CTLIN

           WRITE REG-CLIRELAT FROM WS-RODAPE1 AFTER WS-PULA LINES
           IF NOT SUCESSO
               DISPLAY "ERRO IMPRESSAO WS-RODAPE1"
               DISPLAY FS-CLIRELAT
           END-IF

           WRITE REG-CLIRELAT FROM WS-RODAPE2
           IF NOT SUCESSO
               DISPLAY "ERRO IMPRESSAO WS-RODAPE2"
               DISPLAY FS-CLIRELAT
           END-IF

           MOVE 60 TO WS-CTLIN
           .

       3000-TERMINO.
           DISPLAY '3000-TERMINO'
           IF WS-CTLIN < 60
               PERFORM 2300-IMPRIME-RODAPE
           END-IF

           MOVE WS-CTLIDO                    TO WS-CTLIDO-F
           MOVE WS-CTIMP                     TO WS-CTIMP-F
           MOVE WS-CTPAG                     TO WS-CTPAG-F

           EXEC SQL
               CLOSE CLISEMEMAIL
           END-EXEC

           IF SQLCODE NOT EQUAL 0
               DISPLAY "ERRO AO FECHAR CURSOR"
               DISPLAY SQLCODE
               GO TO 9000-ERRO
           END-IF

           CLOSE CLIRELAT
           IF NOT SUCESSO
               DISPLAY "ERRO FECHAMENTO CLIRELAT"
               DISPLAY FS-CLIRELAT
               GO TO 9000-ERRO
           END-IF

           ACCEPT WS-HORARIO-FINAL FROM TIME
           PERFORM 9000-CALCULA-TEMPO-PROC
           PERFORM 9000-IMPRIME-DATA

           DISPLAY '--------------------------------------------------'
           DISPLAY '                 WEYLAND-YUTANI                   '
           DISPLAY '--------------------------------------------------'
           DISPLAY 'TOTAL DE CLIENTE LIDOS............' WS-CTLIDO-F
           DISPLAY 'TOTAL DE CLIENTE IMPRESSOS........' WS-CTLIN-F
           DISPLAY 'TOTAL DE PAGINAS IMPRESSAS........' WS-CTPAG-F
           DISPLAY '--------------------------------------------------'
           DISPLAY 'TEMPO TOTAL DE PROCESSAMENTO...'
           DISPLAY WS-TEMPO-PROCESSAMENTO
           DISPLAY '=================================================='
           DISPLAY '             TERMINO NORMAL EXERELAT              '
           DISPLAY '=================================================='
           .

       9000-ERRO.
           DISPLAY '9000-ERRO'
           DISPLAY '--------------------------------------------------'
           DISPLAY '   XXX      TERMINO ANORMAL DO EXERELAT     XXX   '
           DISPLAY '--------------------------------------------------'
           STOP RUN
           .

       9000-ERRO-DB2.
           DISPLAY '9000-ERRO-DB2'
           DISPLAY '=================================================='
           DISPLAY 'CODIGO...: ' WS-SQLCODE
           DISPLAY '=================================================='
           .

           COPY ROTDATA.
           COPY ROTPROC.

