      *--------------------------------------------------------------*
       IDENTIFICATION                      DIVISION.
      *--------------------------------------------------------------*
       PROGRAM-ID.                         EXEMANUT.
       AUTHOR.                             FELIPE.
      *
      *--------------------------------------------------------------*
       ENVIRONMENT                         DIVISION.
      *--------------------------------------------------------------*
       CONFIGURATION                       SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.
      *
       INPUT-OUTPUT                        SECTION.
      *
       FILE-CONTROL.
           SELECT MOVPRDD2 ASSIGN TO UT-S-MOVPRDD2
           FILE STATUS IS FS-MOVPRDD2
           .
      *--------------------------------------------------------------*
       DATA                                DIVISION.
      *--------------------------------------------------------------*
       FILE                                SECTION.
      *--------------------------------------------------------------*
       FD  MOVPRDD2
           RECORDING MODE IS F.

           COPY BKPRDMV2 REPLACING ==::== BY ====.

      *--------------------------------------------------------------*
       WORKING-STORAGE                     SECTION.
      *--------------------------------------------------------------*
      * VARIAVEIS ESPELHO DOS REGISTROS                              *
      *--------------------------------------------------------------*
           COPY BKPRDMV2 REPLACING ==::== BY ==WS-==.

           COPY VARDATA.
           COPY VARTIME.

      *    BOOK DAS VARIAVEIS HOSPEDEIRAS
           EXEC SQL
               INCLUDE BKTBCAD
           END-EXEC
           .
      *    INDICADORES DA TABELA TBPRODUTO

       01  INDICADORES.
           05 ICODPROD                      PIC S9(04) COMP.
           05 IDESCPROD                     PIC S9(04) COMP.
           05 IUNIDPROD                     PIC S9(04) COMP.
           05 ILOCALPROD                    PIC S9(04) COMP.
           05 IQTDEST                       PIC S9(04) COMP.
           05 IQTDMAX                       PIC S9(04) COMP.
           05 IQTDMIN                       PIC S9(04) COMP.
           05 IPRECOCOMPRA                  PIC S9(04) COMP.
           05 IPRECOVENDA                   PIC S9(04) COMP.
           05 IPRECOMIS                     PIC S9(04) COMP.

      *    BOOK DAS VARIAVEIS PARA USO DO SQL
           EXEC SQL
               INCLUDE SQLCA
           END-EXEC


      *--------------------------------------------------------------*
      * VARIAVEIS DE FILE STATUS                                     *
      *--------------------------------------------------------------*
       01  FS-MOVPRDD2                     PIC X(02).
           88 SUCESSO-MOV                          VALUE "00".
           88 FIM-ARQ-MOV                          VALUE "10".

       77  WS-SQLCODE                      PIC +9(9).
      *
      *--------------------------------------------------------------*
      * VARIAVEIS DE TRABALHO                                        *
      *--------------------------------------------------------------*
       01  WS-CONTADORES-COMP.
           05  WS-CTLIDO                   PIC 9(05) COMP.
           05  WS-CTINS                    PIC 9(05) COMP.
           05  WS-CTEXC                    PIC 9(05) COMP.
           05  WS-CTINV                    PIC 9(05) COMP.
           05  WS-CTALT                    PIC 9(05) COMP.
           05  WS-CTCON                    PIC 9(05) COMP.
      *--------------------------------------------------------------*
      * VARIAVEIS COM MASCARA DE EDICAO (FORMATADAS)                 *
      *--------------------------------------------------------------*
       01  WS-CONTADORES-COMP-F.
           05  WS-CTLIDO-F                     PIC ZZZ9.
           05  WS-CTINS-F                      PIC ZZZ9.
           05  WS-CTEXC-F                      PIC ZZZ9.
           05  WS-CTINV-F                      PIC ZZZ9.
           05  WS-CTALT-F                      PIC ZZZ9.
           05  WS-CTCON-F                      PIC ZZZ9.
      *
      *--------------------------------------------------------------*

      *--------------------------------------------------------------*
       PROCEDURE                           DIVISION.
      *--------------------------------------------------------------*
       0000-EXEMANU2.
           PERFORM 1000-INICIALIZAR
           PERFORM 2000-PROCESSAR UNTIL FIM-ARQ-MOV
           PERFORM 3000-TERMINO
           STOP RUN
           .

       1000-INICIALIZAR.
           ACCEPT WS-HORARIO-INICIAL FROM TIME

           INITIALIZE WS-CONTADORES-COMP

           OPEN INPUT MOVPRDD2
           IF NOT SUCESSO-MOV
               DISPLAY "DEU RUIM PA ABRIR O ARQUIVO FIO"
               DISPLAY FS-MOVPRDD2
               GO TO 9000-ERRO
           END-IF

           PERFORM 1500-LER-MOVPRDD2
           .

       1500-LER-MOVPRDD2.
           READ MOVPRDD2 INTO WS-REG-MOVPRDD2
           IF SUCESSO-MOV
               ADD 1                         TO WS-CTLIDO
           ELSE
               IF NOT FIM-ARQ-MOV
                   DISPLAY "DEU RUIM PA LER O ARQUIVO FIO"
                   DISPLAY FS-MOVPRDD2
                   GO TO 9000-ERRO
               END-IF
           END-IF
           .

       2000-PROCESSAR.
           EVALUATE WS-TIPOMOVTO-D2
               WHEN "I" PERFORM 2100-INCLUSAO
               WHEN "E" PERFORM 2200-EXCLUSAO
               WHEN "A" PERFORM 2300-ALTERACAO
               WHEN "C" PERFORM 2400-CONSULTAR
               WHEN OTHER
                   ADD 1 TO WS-CTINV
           END-EVALUATE

           PERFORM 1500-LER-MOVPRDD2
           .
      *----------------------------------------------------------------*
      *    MOVER DADOS DA VARIAVELS ESPELHO PARA HOSPEDEIRA            *
      *----------------------------------------------------------------*
       2100-INCLUSAO.
           MOVE WS-CODPROD-D2                TO CODPROD
           MOVE +20                          TO DESCPROD-LEN
           MOVE WS-DESCPROD-D2               TO DESCPROD-TEXT
           MOVE WS-UNIDPROD-D2               TO UNIDPROD
           MOVE WS-LOCALPROD-D2              TO LOCALPROD
           MOVE WS-QTDEST-D2                 TO QTDEST
           MOVE WS-QTDMAX-D2                 TO QTDMAX
           MOVE WS-QTDMIN-D2                 TO QTDMIN
           MOVE WS-PRECOCOMPRA-D2            TO PRECOCOMPRA
           MOVE WS-PRECOVENDA-D2             TO PRECOVENDA
           MOVE WS-PRECOMIS-D2               TO PRECOMIS

      *----------------------------------------------------------------*
      *    INSERIR OS DADOS DA HOSPEDEIRA NA TABELA(SQL FIELDS)        *
      *----------------------------------------------------------------*
           EXEC SQL
               INSERT INTO TBPRODUTO
               (   CODPROD,
                   DESCPROD,
                   UNIDPROD,
                   LOCALPROD,
                   QTDEST,
                   QTDMAX,
                   QTDMIN,
                   PRECOCOMPRA,
                   PRECOVENDA,
                   PRECOMIS
               )
               VALUES
               (   :CODPROD,
                   :DESCPROD,
                   :UNIDPROD,
                   :LOCALPROD,
                   :QTDEST,
                   :QTDMAX,
                   :QTDMIN,
                   :PRECOCOMPRA,
                   :PRECOVENDA,
                   :PRECOMIS
               )
           END-EXEC

      *----------------------------------------------------------------*
      *    TESTAR SQLCODE                                              *
      *----------------------------------------------------------------*
           EVALUATE SQLCODE
           WHEN 0
               ADD 1 TO WS-CTINS
           WHEN -803
               ADD 1 TO WS-CTINV
           WHEN -545
               ADD 1 TO WS-CTINV
           WHEN OTHER
               DISPLAY "ERRO INSERT TBPRODUTO"
               DISPLAY SQLCODE
               GO TO 9000-ERRO-DB2
           END-EVALUATE

           PERFORM 1500-LER-MOVPRDD2
           .

       2200-EXCLUSAO.
           MOVE WS-CODPROD-D2                 TO CODPROD

           EXEC SQL
               DELETE FROM TBPRODUTO
               WHERE CODPROD = :CODPROD
           END-EXEC

           EVALUATE SQLCODE
               WHEN 0    ADD 1 TO WS-CTEXC
               WHEN +100 ADD 1 TO WS-CTINV
               WHEN OTHER
                   DISPLAY "ERRO DELETE NA TBPRODUTO"
                   DISPLAY SQLCODE
                   PERFORM 9000-ERRO-DB2
           END-EVALUATE
           .

       2300-ALTERACAO.
           MOVE WS-CODPROD-D2                 TO CODPROD

           EXEC SQL
               SELECT
                   CODPROD,
                   DESCPROD,
                   UNIDPROD,
                   LOCALPROD,
                   QTDEST,
                   QTDMAX,
                   QTDMIN,
                   PRECOCOMPRA,
                   PRECOVENDA,
                   PRECOMIS

               INTO :CODPROD,
                    :DESCPROD,
                    :UNIDPROD    :IUNIDPROD,
                    :LOCALPROD   :ILOCALPROD,
                    :QTDEST      :IQTDEST,
                    :QTDMAX      :IQTDMAX,
                    :QTDMIN      :IQTDMIN,
                    :PRECOCOMPRA :IPRECOCOMPRA,
                    :PRECOVENDA  :IPRECOVENDA,
                    :PRECOMIS    :IPRECOMIS
               FROM TBPRODUTO
               WHERE CODPROD = :CODPROD
           END-EXEC

           EVALUATE SQLCODE
               WHEN 0
                   PERFORM 9000-TRATA-INDICATOR
                   PERFORM 2310-UPDATE
               WHEN +100
                   ADD 1 TO WS-CTINV
               WHEN OTHER
                   DISPLAY "ERRO SELECT DO UPDATE 2300"
                   DISPLAY SQLCODE
                   PERFORM 9000-ERRO-DB2
           END-EVALUATE
           .

       2310-UPDATE.
      *    TESTE DA VARIAVEL ESPELHO
           IF WS-descprod-d2 NOT = SPACES
               MOVE +20                       TO DESCPROD-LEN
               MOVE WS-descprod-d2            TO DESCPROD-TEXT
           END-IF

           IF WS-uniDprod-d2  NOT = SPACES
               MOVE WS-UNIDPROD-D2            TO UNIDPROD
           END-IF

           IF WS-localprod-d2 NOT = SPACES
               MOVE WS-localprod-d2           TO LOCALPROD
           END-IF

           IF WS-qtdest-d2   IS NUMERIC
               MOVE WS-QTDEST-D2              TO QTDEST
           END-IF

           IF WS-qtdmin-d2   IS NUMERIC
               MOVE WS-QTDMIN-D2              TO QTDMIN
           END-IF

           IF WS-qtdmax-d2   IS NUMERIC
               MOVE WS-QTDMAX-D2              TO QTDMAX
           END-IF

           IF WS-precocompra-D2 IS NUMERIC
               MOVE WS-PRECOCOMPRA-D2         TO PRECOCOMPRA
           END-IF

           IF WS-precovenda-D2 IS NUMERIC
               MOVE WS-PRECOVENDA-D2          TO PRECOVENDA
           END-IF

           IF WS-precomis-D2 IS NUMERIC
               MOVE WS-PRECOMIS-D2            TO PRECOMIS
           END-IF

           EXEC SQL
               UPDATE TBPRODUTO
               SET DESCPROD    = :DESCPROD,
                   UNIDPROD    = :UNIDPROD,
                   LOCALPROD   = :LOCALPROD,
                   QTDEST      = :QTDEST,
                   QTDMAX      = :QTDMAX,
                   QTDMIN      = :QTDMIN,
                   PRECOCOMPRA = :PRECOCOMPRA,
                   PRECOVENDA  = :PRECOVENDA,
                   PRECOMIS    = :PRECOMIS
           WHERE CODPROD       = :CODPROD
           END-EXEC

           EVALUATE SQLCODE
               WHEN 0      ADD 1 TO WS-CTALT
               WHEN +100   ADD 1 TO WS-CTINV
               WHEN -545   ADD 1 TO WS-CTINV
               WHEN OTHER
                   DISPLAY "ERRO UPDATE TABELA 2310"
                   MOVE SQLCODE TO WS-SQLCODE
                   PERFORM 9000-ERRO-DB2
           END-EVALUATE
           .

       2400-CONSULTAR.
           MOVE WS-CODprod-d2                 TO CODPROD

           EXEC SQL
               SELECT
                   CODPROD,
                   DESCPROD,
                   UNIDPROD,
                   LOCALPROD,
                   QTDEST,
                   QTDMAX,
                   QTDMIN,
                   PRECOCOMPRA,
                   PRECOVENDA,
                   PRECOMIS
               INTO :CODPROD,
                    :DESCPROD    :IDESCPROD,
                    :UNIDPROD    :IUNIDPROD,
                    :LOCALPROD   :ILOCALPROD,
                    :QTDEST      :IQTDEST,
                    :QTDMAX      :IQTDMAX,
                    :QTDMIN      :IQTDMIN,
                    :PRECOCOMPRA :IPRECOCOMPRA,
                    :PRECOVENDA  :IPRECOVENDA,
                    :PRECOMIS    :IPRECOMIS
               FROM TBPRODUTO
               WHERE CODPROD = :CODPROD
           END-EXEC

           EVALUATE SQLCODE
               WHEN 0 ADD 1 TO WS-CTCON
                   PERFORM 9000-TRATA-INDICATOR
                   PERFORM 9000-EXIBE-CONSULTA
               WHEN +100 ADD 1 TO WS-CTINV
               WHEN OTHER
                   DISPLAY "ERRO CONSULTA TBPRODUTO 2400"
                   DISPLAY SQLCODE
                   PERFORM 9000-ERRO-DB2
           END-EVALUATE
           .

       3000-TERMINO.
           CLOSE MOVPRDD2
           IF NOT SUCESSO-MOV
               DISPLAY "DEU POBREMA NO FECHAMENTO PARCEIRO"
               DISPLAY FS-MOVPRDD2
               GO TO 9000-ERRO
           END-IF

           ACCEPT WS-HORARIO-FINAL FROM TIME

           PERFORM 9000-CALCULA-TEMPO-PROC

           MOVE WS-CTLIDO                     TO WS-CTLIDO-F
           MOVE WS-CTINS                      TO WS-CTINS-F
           MOVE WS-CTEXC                      TO WS-CTEXC-F
           MOVE WS-CTINV                      TO WS-CTINV-F
           MOVE WS-CTALT                      TO WS-CTALT-F
           MOVE WS-CTCON                      TO WS-CTCON-F

           PERFORM 9000-IMPRIME-DATA


           DISPLAY '=================================================='
           DISPLAY '        ESTATISCA FINAL DE PROCESSAMENTO          '
           DISPLAY '=================================================='
           DISPLAY 'QTDE. PRODUTOS LIDOS MOVPRDD2.......: ' WS-CTLIDO-F
           DISPLAY 'QTDE. PRODUTOS INSERIDOS TBPRODUTO..: ' WS-CTINS-F
           DISPLAY 'QTDE. PRODUTOS EXCLUIDOS TBPRODUTO..: ' WS-CTEXC-F
           DISPLAY 'QTDE. PRODUTOS ALTERADOS TBPRODUTO..: ' WS-CTALT-F
           DISPLAY 'QTDE. PRODUTOS CONSULTADOS..........: ' WS-CTCON-F
           DISPLAY 'QTDE. MOVIMENTOS INVALIDOS..........: ' WS-CTINV-F
           DISPLAY '=================================================='
           DISPLAY 'TEMPO TOTAL DE PROCESSAMENTO...'
           DISPLAY WS-TEMPO-PROCESSAMENTO
           DISPLAY '=================================================='
           DISPLAY '             TERMINO NORMAL EXEMANU2              '
           DISPLAY '=================================================='
           .
      *
       9000-TRATA-INDICATOR.
           IF IUNIDPROD LESS ZERO
               MOVE SPACES                     TO UNIDPROD
           END-IF

           IF ILOCALPROD LESS ZERO
               MOVE SPACES                     TO LOCALPROD
           END-IF

           IF IQTDEST LESS ZERO
               MOVE ZEROES                     TO QTDEST
           END-IF

           IF IQTDMAX LESS ZERO
               MOVE ZEROES                     TO QTDMAX
           END-IF

           IF IQTDMIN LESS ZERO
               MOVE ZEROES                     TO QTDMIN
           END-IF

           IF IPRECOCOMPRA LESS ZERO
               MOVE ZEROES                     TO PRECOCOMPRA
           END-IF

           IF IPRECOVENDA LESS ZERO
               MOVE ZEROES                     TO PRECOVENDA
           END-IF

           IF IPRECOMIS LESS ZERO
               MOVE ZEROES                     TO PRECOMIS
           END-IF
           .
       9000-EXIBE-CONSULTA.
           DISPLAY "=================================================="
           DISPLAY '==             CONSULTA DE PRODUTO              =='
           DISPLAY "=================================================="
           DISPLAY 'CODIGO......' CODPROD
           DISPLAY 'DESCRICAO...' DESCPROD-TEXT
           DISPLAY 'UNIDADE.....' UNIDPROD
           DISPLAY 'LOCAL.......' LOCALPROD
           DISPLAY 'QTDEST......' QTDEST
           DISPLAY 'QTDMAX......' QTDMAX
           DISPLAY 'QTDMIN......' QTDMIN
           DISPLAY 'PRECO COMPRA R$ ' PRECOCOMPRA
           DISPLAY 'PRECO VENDA  R$ ' PRECOVENDA
           DISPLAY 'PRECO MIS.   R$ ' PRECOMIS
           DISPLAY "=================================================="
           .
       9000-ERRO-DB2.
           DISPLAY '=================================================='
           DISPLAY 'CODIGO...: ' WS-SQLCODE
           DISPLAY '=================================================='
           .

       9000-ERRO.
           DISPLAY '=================================================='
           DISPLAY '==        TERMINO ANORMAL DO EXEMANU2           =='
           DISPLAY '=================================================='
           STOP RUN
           .

           COPY ROTDATA.
           COPY ROTPROC.
