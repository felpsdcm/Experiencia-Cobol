      ******************************************************************
      *                                                                *
      *          I D E N T I F I C A T I O N   D I V I S I O N         *
      *                                                                *
      ******************************************************************
       IDENTIFICATION                  DIVISION.
          PROGRAM-ID.                  E1033512.
          AUTHOR                       FELIPE.
      ******************************************************************
      *                                                                *
      *          E N V I R O N M E N T         D I V I S I O N         *
      *                                                                *
      ******************************************************************
       ENVIRONMENT                     DIVISION.

      *-----------------------*
      * CONFIGURATION SECTION *
      *----------------------------------------------------------------*
       CONFIGURATION                   SECTION.
       SPECIAL-NAMES.
           DECIMAL-POINT IS COMMA.

      *----------------------*
      * INPUT OUTPUT SECTION *
      *----------------------------------------------------------------*
       INPUT-OUTPUT                    SECTION.

       FILE-CONTROL.

           SELECT FILE005 ASSIGN TO FILE005
                  FILE STATUS IS WRK-FS-FILE5.

           SELECT FILE007 ASSIGN TO FILE007
                  FILE STATUS IS WRK-FS-FILE7.

           SELECT FILE008 ASSIGN TO FILE008
                  FILE STATUS IS WRK-FS-FILE8.

      ******************************************************************
      *                                                                *
      *          D A T A                       D I V I S I O N         *
      *                                                                *
      ******************************************************************
       DATA                            DIVISION.
      *
      *---------------*
      * FILE SECTION  *
      *----------------------------------------------------------------*
       FILE SECTION.

       FD  FILE005
           RECORDING MODE IS F
           LABEL RECORD IS STANDARD
           BLOCK CONTAINS 364 RECORDS
           DATA RECORD IS FD-FILE5-REGISTRO.
       01  REG-FILE5.
           COPY B135F005.

       FD  FILE007
           RECORDING MODE IS F
           LABEL RECORD IS STANDARD
           BLOCK CONTAINS 61 RECORDS
           DATA RECORD IS FD-FILE7-REGISTRO.
       01  REG-FILE7.
           COPY B135F007.

       FD  FILE008
           RECORDING MODE IS F
           LABEL RECORD IS STANDARD
           BLOCK CONTAINS 374 RECORDS
           DATA RECORD IS FD-FILE5-REGISTRO.
       01  REG-FILE8.
           COPY B135F008.

      *-------------------------*
      * WORKING STORAGE SECTION *
      *----------------------------------------------------------------*
       WORKING-STORAGE                 SECTION.

      * FLAGS
       01  WRK-FLAGS-SWITCHES.
           05  WRK-DEBUG               PIC  X(01)  VALUE 'N'.
               88  WRK-DEBUG-NAO                   VALUE 'N'.
               88  WRK-DEBUG-SIM                   VALUE 'S'.

      * FILE STATUS
       01  WRK-STATUS.

           05  WRK-FS-FILE5            PIC  X(02)  VALUE SPACES.
               88  WRK-FS-FILE5-OK                 VALUE '00'.
               88  WRK-FS-FILE5-EOF                VALUE '10'.

           05  WRK-FS-FILE7            PIC  X(02)  VALUE SPACES.
               88  WRK-FS-FILE7-OK                 VALUE '00'.
               88  WRK-FS-FILE7-EOF                VALUE '10'.

           05  WRK-FS-FILE8            PIC  X(02)  VALUE SPACES.
               88  WRK-FS-FILE8-OK                 VALUE '00'.
               88  WRK-FS-FILE8-EOF                VALUE '10'.

       01  WS-AUX-HORAINICIO.
           05 AUX-DIAI               PIC 9(2).
           05 FILLER                 PIC X(1).
           05 AUX-MESI               PIC 9(2).
           05 FILLER                 PIC X(1).
           05 AUX-ANOI               PIC 9(4).
           05 AUX-HORAI              PIC 9(2).
           05 FILLER                 PIC X(1).
           05 AUX-MINI               PIC 9(2).
           05 FILLER                 PIC X(1).
           05 AUX-SEGI               PIC 9(2).
           05 FILLER                 PIC X(1).
           05 AUX-MSI                PIC 9(4).

       01  WS-AUX-HORAFIM.
           05 AUX-DIAF               PIC 9(2).
           05 FILLER                 PIC X(1).
           05 AUX-MESF               PIC 9(2).
           05 FILLER                 PIC X(1).
           05 AUX-ANOF               PIC 9(4).
           05 AUX-HORAF              PIC 9(2).
           05 FILLER                 PIC X(1).
           05 AUX-MINF               PIC 9(2).
           05 FILLER                 PIC X(1).
           05 AUX-SEGF               PIC 9(2).
           05 FILLER                 PIC X(1).
           05 AUX-MSF                PIC 9(4).

       77  WS-HORAINICIAL            PIC 9(04)V99.
       77  WS-HORAFINAL              PIC 9(04)V99.

       77  WS-VALOR-HORA-EXTRA       PIC S9(08)V99 COMP-3.
       77  WS-QTD-EXTRATOTAL         PIC 9(03)V99.
       77  WS-QTD-EXTRA              PIC 9(03)V99.
       77  WS-HRAINICIO              PIC 9(03)V99.
       77  WS-HRAFIM                 PIC 9(03)V99.

      * COPYBOOKS
       01  WRK-BKALERRO.
           COPY BKALERRO.
      ******************************************************************
      *                                                                *
      *          P R O C E D U R E             D I V I S I O N         *
      *                                                                *
      ******************************************************************
       PROCEDURE                       DIVISION.
      *----------------------------------------------------------------*
      * 0000-PRINCIPAL: ESTRUTURA PRINCIPAL DO PROGRAMA.               *
      *----------------------------------------------------------------*
       0000-MAIN                       SECTION.

           IF  WRK-DEBUG-SIM
               DISPLAY '0000-MAINLINE'
           END-IF

           PERFORM 1000-INICIAR
              THRU 1000-INICIAR-FIM

           PERFORM 2000-PROCESSAR
              THRU 2000-PROCESSAR-FIM
             UNTIL WRK-FS-FILE5-EOF AND
                   WRK-FS-FILE7-EOF

           PERFORM 3000-FINALIZAR
              THRU 3000-FINALIZAR-FIM

           STOP RUN.

       0000-FIM.                       EXIT.
      *----------------------------------------------------------------*
      * 1000-INICAR: TODOS OS PROCEDIMENTOS PARA INICIAR O PROGRAMA DA *
      *    FORMA CORRETA COMO: ABRIR ARQUIVOS                          *
      *----------------------------------------------------------------*
       1000-INICIAR                    SECTION.

           IF  WRK-DEBUG-SIM
               DISPLAY ' 1000-INICIAR'
           END-IF

           PERFORM 1100-ABRIR-ARQUIVOS
              THRU 1100-ABRIR-ARQUIVOS-FIM

           PERFORM 9100-LER-FILE5
              THRU 9100-LER-FILE5-FIM.

           PERFORM 9200-LER-FILE7
              THRU 9200-LER-FILE7-FIM.

       1000-INICIAR-FIM.               EXIT.
      *----------------------------------------------------------------*
      * 1100-ABRIR-ARQUIVOS: REALIZA ABERTURA DOS ARQUIVOS A SEREM UTI-*
      *    LIZADOS NO PROGRAMA REALIZANDO OS TRATAMENTOS EM CASO DE    *
      *    ERRO                                                        *
      *----------------------------------------------------------------*
       1100-ABRIR-ARQUIVOS             SECTION.

           IF  WRK-DEBUG-SIM
               DISPLAY '  1100-ABRIR-ARQUIVOS'
           END-IF

           OPEN INPUT FILE005

           IF  NOT  WRK-FS-FILE5-OK
               MOVE 'PARAGRAFO 1100'   TO BKALERRO-PARAGRAFO
               MOVE 'FILE5'            TO BKALERRO-ARQ-NOME
               MOVE 'ABERTURA'         TO BKALERRO-ARQ-MOMENTO
               MOVE WRK-FS-FILE5       TO BKALERRO-ARQ-FILESTATUS

               PERFORM 9999-ABEND
                  THRU 9999-ABEND-FIM

           END-IF

           OPEN INPUT FILE007

           IF  NOT  WRK-FS-FILE7-OK
               MOVE 'PARAGRAFO 1100'   TO BKALERRO-PARAGRAFO
               MOVE 'FILE7'            TO BKALERRO-ARQ-NOME
               MOVE 'ABERTURA'         TO BKALERRO-ARQ-MOMENTO
               MOVE WRK-FS-FILE7       TO BKALERRO-ARQ-FILESTATUS

               PERFORM 9999-ABEND
                  THRU 9999-ABEND-FIM

           END-IF

           OPEN OUTPUT FILE008

           IF  NOT  WRK-FS-FILE8-OK
               MOVE 'PARAGRAFO 1100'   TO BKALERRO-PARAGRAFO
               MOVE 'FILE8'            TO BKALERRO-ARQ-NOME
               MOVE 'ABERTURA'         TO BKALERRO-ARQ-MOMENTO
               MOVE WRK-FS-FILE8       TO BKALERRO-ARQ-FILESTATUS

               PERFORM 9999-ABEND
                  THRU 9999-ABEND-FIM

           END-IF.

       1100-ABRIR-ARQUIVOS-FIM.        EXIT.
      *----------------------------------------------------------------*
      * 2000-PROCESSAR TODA A REGRA DE NEGOCIO DEVE ESTAR PRESENTE NES-*
      *    SE PARAGRAFO                                                *
      *----------------------------------------------------------------*
       2000-PROCESSAR                  SECTION.

           IF  WRK-DEBUG-SIM
               DISPLAY ' 2000-PROCESSAR'
           END-IF

           DISPLAY 'REGISTRO DO ARQUIVO: ' REG-FILE5

           DISPLAY 'REGISTRO DO ARQUIVO: ' REG-FILE7

           IF B135F005-CPF < B135F007-CPF

             MOVE B135F005-CPF              TO B135F008-CPF
             MOVE B135F005-NOME             TO B135F008-NOME
             MOVE B135F005-TELEFONE         TO B135F008-TELEFONE
             MOVE B135F005-ID-CARGO         TO B135F008-ID-CARGO
             MOVE B135F005-ID-DEPARTAMENTO  TO B135F008-ID-DEPARTAMENTO
             MOVE B135F005-SALARIO-BASE     TO B135F008-SALARIO-BASE
             MOVE B135F005-DATA-ADMISSAO    TO B135F008-DATA-ADMISSAO
             MOVE B135F005-DATA-DEMISSAO    TO B135F008-DATA-DEMISSAO
             MOVE B135F005-QTD-DEPENDENTES  TO B135F008-QTD-DEPENDENTES
             MOVE B135F005-SOLICITOU-VT     TO B135F008-SOLICITOU-VT
             MOVE B135F005-CONTRIB-SINDICAL TO B135F008-CONTRIB-SINDICAL
             MOVE WS-QTD-EXTRATOTAL         TO B135F008-QTD-HORAS-EXTRAS

                   PERFORM 9500-COMPUTE-VALOR-EXTRAS
                      THRU 9500-COMPUTE-VALOR-EXTRAS-FIM

           MOVE WS-VALOR-HORA-EXTRA       TO B135F008-VALOR-HORA-EXTRA

                   PERFORM 9300-GRAVAR
                      THRU 9300-GRAVAR-FIM

               MOVE ZEROS                     TO WS-AUX-HORAFIM
               MOVE ZEROS                     TO WS-AUX-HORAINICIO
               MOVE ZEROS                     TO WS-HORAFINAL
               MOVE ZEROS                     TO WS-QTD-EXTRA
               MOVE ZEROS                     TO WS-QTD-EXTRATOTAL
               MOVE ZEROS                     TO WS-VALOR-HORA-EXTRA

                   PERFORM 9100-LER-FILE5
                      THRU 9100-LER-FILE5-FIM

           ELSE
               IF B135F005-CPF > B135F007-CPF

                   IF WRK-FS-FILE5-EOF
                       MOVE 'HORA EXTRA SEM FUNCIONARIO'
                                             TO BKALERRO-ARQ-MENSAGEM

                       PERFORM 9999-ABEND
                          THRU 9999-ABEND-FIM
                   ELSE

                       MOVE B135F005-CPF              TO B135F008-CPF
                       MOVE B135F005-NOME             TO B135F008-NOME
                       MOVE B135F005-TELEFONE
                       TO B135F008-TELEFONE

                       MOVE B135F005-ID-CARGO
                       TO B135F008-ID-CARGO

                       MOVE B135F005-ID-DEPARTAMENTO
                       TO B135F008-ID-DEPARTAMENTO

                       MOVE B135F005-SALARIO-BASE
                       TO B135F008-SALARIO-BASE

                       MOVE B135F005-DATA-ADMISSAO
                       TO B135F008-DATA-ADMISSAO

                       MOVE B135F005-DATA-DEMISSAO
                       TO B135F008-DATA-DEMISSAO

                       MOVE B135F005-QTD-DEPENDENTES
                       TO B135F008-QTD-DEPENDENTES

                       MOVE B135F005-SOLICITOU-VT
                       TO B135F008-SOLICITOU-VT

                       MOVE B135F005-CONTRIB-SINDICAL
                       TO B135F008-CONTRIB-SINDICAL

                       MOVE ZEROS
                       TO B135F008-QTD-HORAS-EXTRAS
                          B135F008-VALOR-HORA-EXTRA

                       PERFORM 9300-GRAVAR
                          THRU 9300-GRAVAR-FIM

                       PERFORM 9100-LER-FILE5
                          THRU 9100-LER-FILE5-FIM

                   END-IF

               ELSE
                   MOVE B135F007-DATA-HORA-INICIO TO WS-AUX-HORAINICIO
                   MOVE B135F007-DATA-HORA-FIM    TO WS-AUX-HORAFIM

                   PERFORM 9400-COMPUTE-EXTRA
                      THRU 9400-COMPUTE-EXTRA-FIM

                   PERFORM 9200-LER-FILE7
                      THRU 9200-LER-FILE7-FIM
               END-IF

           END-IF
           .

       2000-PROCESSAR-FIM.             EXIT.
      *----------------------------------------------------------------*
      * 3000-FINALIZAR: SAO REALIZADOS TODOS OS PROCEDIMENTOS PARA FI- *
      *    NALIZAR O PROGRAMA DA FORMA CORRETA. OS ARQUIVOS SAO FECHA- *
      *    DOS E A ESTATISTICA DO PROGRAMA EH REALIZADA                *
      *----------------------------------------------------------------*
       3000-FINALIZAR                  SECTION.

           IF  WRK-DEBUG-SIM
               DISPLAY ' 3000-FINALIZAR'
           END-IF

           PERFORM 3100-FECHAR-ARQUIVOS
              THRU 3100-FECHAR-ARQUIVOS-FIM.

       3000-FINALIZAR-FIM.             EXIT.
      *----------------------------------------------------------------*
      * 3100-FECHAR ARQUIVOS: TODOS OS ARQUIVOS DEVEM SER FECHADOS COM *
      *    OS RESPECTIVOS TRATAMENTOS DE FILE STATUS REALIZADOS        *
      *----------------------------------------------------------------*
       3100-FECHAR-ARQUIVOS            SECTION.

           IF  WRK-DEBUG-SIM
               DISPLAY '  3100-FECHAR-ARQUIVOS'
           END-IF

           CLOSE FILE005.

           IF  NOT  WRK-FS-FILE5-OK

               MOVE '3100-FECHAR-ARQUIVOS'
                                       TO BKALERRO-PARAGRAFO
               MOVE 'FILE5'            TO BKALERRO-ARQ-NOME
               MOVE 'FECHAMENTO'       TO BKALERRO-ARQ-MOMENTO
               MOVE WRK-FS-FILE5       TO BKALERRO-ARQ-FILESTATUS

               PERFORM 9999-ABEND
                  THRU 9999-ABEND-FIM
           END-IF

           CLOSE FILE007.

           IF  NOT  WRK-FS-FILE7-OK

               MOVE '3100-FECHAR-ARQUIVOS'
                                       TO BKALERRO-PARAGRAFO
               MOVE 'FILE7'            TO BKALERRO-ARQ-NOME
               MOVE 'FECHAMENTO'       TO BKALERRO-ARQ-MOMENTO
               MOVE WRK-FS-FILE7       TO BKALERRO-ARQ-FILESTATUS

               PERFORM 9999-ABEND
                  THRU 9999-ABEND-FIM
           END-IF

           CLOSE FILE008.

           IF  NOT  WRK-FS-FILE8-OK

               MOVE '3100-FECHAR-ARQUIVOS'
                                       TO BKALERRO-PARAGRAFO
               MOVE 'FILE8'            TO BKALERRO-ARQ-NOME
               MOVE 'FECHAMENTO'       TO BKALERRO-ARQ-MOMENTO
               MOVE WRK-FS-FILE8       TO BKALERRO-ARQ-FILESTATUS

               PERFORM 9999-ABEND
                  THRU 9999-ABEND-FIM
           END-IF
           .

       3100-FECHAR-ARQUIVOS-FIM.       EXIT.
      *----------------------------------------------------------------*
      * 9100-LER-ARQUIVO                                               *
      * DESC: TODOS OS PROCEDIMENTOS PARA INICIAR O PROGRAMA DA FORMA  *
      *       CORRETA COMO: ABRIR ARQUIVOS                             *
      *----------------------------------------------------------------*
       9100-LER-FILE5                  SECTION.

           IF  WRK-DEBUG-SIM
               DISPLAY ' 9100-LER-FILE5'
           END-IF

           READ FILE005

           IF  NOT WRK-FS-FILE5-OK
           AND NOT WRK-FS-FILE5-EOF

               MOVE 'FILE5'            TO BKALERRO-ARQ-NOME
               MOVE 'LEITURA'          TO BKALERRO-ARQ-MOMENTO
               MOVE WRK-FS-FILE5       TO BKALERRO-ARQ-FILESTATUS

               PERFORM 9999-ABEND
                  THRU 9999-ABEND-FIM
           ELSE
               IF WRK-FS-FILE5-EOF AND NOT WRK-FS-FILE7-EOF
                 MOVE LOW-VALUES TO B135F005-CPF
               END-IF

               IF B135F005-TELEFONE          IS NOT NUMERIC OR
                  B135F005-ID-CARGO          IS NOT NUMERIC OR
                  B135F005-ID-DEPARTAMENTO   IS NOT NUMERIC OR
                  B135F005-SALARIO-BASE      IS NOT NUMERIC OR
                  B135F005-QTD-DEPENDENTES   IS NOT NUMERIC OR
                  B135F005-SOLICITOU-VT      IS NOT NUMERIC OR
                  B135F005-CONTRIB-SINDICAL  IS NOT NUMERIC

                   MOVE 'EXISTEM CAMPOS NUMERICOS SUJOS'
                                         TO BKALERRO-ARQ-MENSAGEM
                   MOVE '9100-LER-FILE5' TO BKALERRO-PARAGRAFO
                   MOVE 'FILE5'          TO BKALERRO-ARQ-NOME
                   MOVE 'LEITURA'        TO BKALERRO-ARQ-MOMENTO
                   MOVE WRK-FS-FILE5     TO BKALERRO-ARQ-FILESTATUS


                   PERFORM 9999-ABEND
                      THRU 9999-ABEND-FIM
               END-IF
           END-IF
           .

       9100-LER-FILE5-FIM.             EXIT.

       9200-LER-FILE7                  SECTION.

           IF  WRK-DEBUG-SIM
               DISPLAY ' 9100-LER-FILE7'
           END-IF

           READ FILE007

           IF  NOT WRK-FS-FILE7-OK
           AND NOT WRK-FS-FILE7-EOF

               MOVE 'FILE7'            TO BKALERRO-ARQ-NOME
               MOVE 'LEITURA'          TO BKALERRO-ARQ-MOMENTO
               MOVE WRK-FS-FILE7       TO BKALERRO-ARQ-FILESTATUS

               PERFORM 9999-ABEND
                  THRU 9999-ABEND-FIM

           ELSE
               IF WRK-FS-FILE7-EOF AND NOT WRK-FS-FILE5-EOF
                 MOVE HIGH-VALUES TO B135F007-CPF
               END-IF

           END-IF
           .

       9200-LER-FILE7-FIM.             EXIT.

       9300-GRAVAR                     SECTION.

           WRITE REG-FILE8

           IF NOT WRK-FS-FILE5-OK
               MOVE '9300-GRAVAR'         TO BKALERRO-PARAGRAFO
               MOVE 'FILE8'               TO BKALERRO-ARQ-NOME
               MOVE 'GRAVACAO'            TO BKALERRO-ARQ-MOMENTO
               MOVE WRK-FS-FILE8          TO BKALERRO-ARQ-FILESTATUS

               PERFORM 9999-ABEND
               THRU 9999-ABEND-FIM
           END-IF
           .

       9300-GRAVAR-FIM.                EXIT.

       9400-COMPUTE-EXTRA              SECTION.
           COMPUTE WS-HORAINICIAL =
                   AUX-HORAI + (AUX-MINI / 60) + (AUX-SEGI / 3600) +
                   (AUX-SEGI / 3600000)

           DISPLAY 'HORA INICIAL' WS-HORAINICIAL

           COMPUTE WS-HORAFINAL =
                   AUX-HORAF + (AUX-MINF / 60) + (AUX-SEGF / 3600) +
                   (AUX-SEGF / 3600000)

           DISPLAY 'HORA INIFINAL' WS-HORAFINAL

           COMPUTE WS-QTD-EXTRA = WS-HORAINICIAL - WS-HORAFINAL

           ADD WS-QTD-EXTRA TO WS-QTD-EXTRATOTAL
           DISPLAY 'HORA TOTAL' WS-QTD-EXTRATOTAL
           .

       9400-COMPUTE-EXTRA-FIM.         EXIT.

       9500-COMPUTE-VALOR-EXTRAS       SECTION.

           IF B135F005-ID-CARGO IS EQUAL TO 0001
               COMPUTE WS-VALOR-HORA-EXTRA =
               ((B135F005-SALARIO-BASE / 30) * 0,20) * WS-QTD-EXTRATOTAL

           END-IF

           IF B135F005-ID-CARGO IS EQUAL TO 0002
               COMPUTE WS-VALOR-HORA-EXTRA =
               ((B135F005-SALARIO-BASE / 30) * 0,40) * WS-QTD-EXTRATOTAL

           END-IF

           IF B135F005-ID-CARGO IS EQUAL TO 0003
               COMPUTE WS-VALOR-HORA-EXTRA =
               ((B135F005-SALARIO-BASE / 30) * 0,55) * WS-QTD-EXTRATOTAL


           END-IF

           IF B135F005-ID-CARGO IS NOT EQUAL TO 0001 AND
                                   NOT EQUAL TO 0002 AND
                                   NOT EQUAL TO 0003

           MOVE '9500-COMPUTE-VALOR-EXTRAS' TO BKALERRO-PARAGRAFO
           MOVE 'DEPARTAMENTO NAO ENCONTRADO'
                                            TO BKALERRO-ARQ-MOMENTO

            PERFORM 9999-ABEND
               THRU 9999-ABEND-FIM
           END-IF
           .

       9500-COMPUTE-VALOR-EXTRAS-FIM.      EXIT.
      *----------------------------------------------------------------*
      * 9999-ABEND                                                     *
      * DESC: UMA VEZ QUE O DIMENTOS PARA FINALIZAR O PROGRAMA DA FOR- *
      *       MA CORRETA COMO: FECHAR ARQUIVOS                         *
      *----------------------------------------------------------------*
       9999-ABEND                      SECTION.

           IF  WRK-DEBUG-SIM
               DISPLAY '9999-ABEND'
           END-IF

           DISPLAY '***************************************'
           DISPLAY '*                ERRO                 *'
           DISPLAY '***************************************'
           DISPLAY 'MENSAGEM           : ' BKALERRO-ARQ-MENSAGEM
           DISPLAY 'PARAGRAFO          : ' BKALERRO-PARAGRAFO
           DISPLAY 'ARQUIVO            : ' BKALERRO-ARQ-NOME
           DISPLAY 'ACAO               : ' BKALERRO-ARQ-MOMENTO
           DISPLAY 'FILE STATUS        : ' BKALERRO-ARQ-FILESTATUS
           DISPLAY 'REGISTRO DO ARQUIVO: ' REG-FILE5
           DISPLAY '***************************************'

           COMPUTE BKALERRO-RESULTADO =
                   BKALERRO-NUMERADOR / BKALERRO-DENOMINADOR
           .
       9999-ABEND-FIM.                 EXIT.
